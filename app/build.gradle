apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'

// load our keystore configuration
def keystore = new Properties()
keystore.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "io.rapidpro.surveyor"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1342
        versionName "13.4.2"
        multiDexEnabled = true

        testInstrumentationRunner "io.rapidpro.surveyor.test.TestRunner"
        testInstrumentationRunnerArgument 'notAnnotation', 'androidx.test.filters.FlakyTest'
    }
    signingConfigs {
        release {
            storeFile file(keystore['storeFile'])
            storePassword keystore['storePassword']
            keyAlias keystore['keyAlias']
            keyPassword keystore['keyPassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            testCoverageEnabled true
        }
    }
    flavorDimensions 'version'
    productFlavors {
        rapidpro {
            dimension 'version'
        }
        "$thirdparty.applicationId" {
            dimension 'version'
            applicationId thirdparty.applicationId
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
    google()
}

dependencies {
    implementation project(':goflow')

    // google services
    //noinspection GradleDependency issues with 1.1.1 https://issuetracker.google.com/issues/141132133
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.exifinterface:exifinterface:1.1.0'

    // rest api
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // for location services
    implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    // internationalization, language iso codes
    implementation 'com.neovisionaries:nv-i18n:1.11'

    // apache commons
    implementation  'org.apache.commons:commons-lang3:3.8.1'
    //noinspection GradleDependency newer versions use java.nio methods not supported on older Android versions
    implementation 'commons-io:commons-io:2.4'

    implementation 'com.vdurmont:semver4j:2.2.0'

    // Android M permissions
    implementation 'com.greysonparrelli.permiso:permiso:0.3.0'

    // unit tests
    testImplementation 'junit:junit:4.12'

    // instrumented tests
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.2.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
}

task jacocoMergedReport(type: JacocoReport, dependsOn: ['testRapidproDebugUnitTest', 'createRapidproDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testRapidproDebugUnitTest.exec",
            "outputs/code_coverage/rapidproDebugAndroidTest/connected/*coverage.ec"
    ])
}
