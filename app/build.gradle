apply plugin: 'com.android.application'
apply plugin: 'com.hiya.jacoco-android'

// load our keystore configuration
def keystore = new Properties()
keystore.load(new FileInputStream(rootProject.file("keystore.properties")))

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "io.rapidpro.surveyor"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1401
        versionName "14.0.1"
        multiDexEnabled = true

        testInstrumentationRunner "io.rapidpro.surveyor.test.TestRunner"
        testInstrumentationRunnerArguments notAnnotation: 'androidx.test.filters.FlakyTest'
    }
    signingConfigs {
        release {
            storeFile file(keystore['storeFile'])
            storePassword keystore['storePassword']
            keyAlias keystore['keyAlias']
            keyPassword keystore['keyPassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            testCoverageEnabled true
        }
    }
    flavorDimensions 'version'
    productFlavors {
        rapidpro {
            dimension 'version'
        }
        "$thirdparty.applicationId" {
            dimension 'version'
            applicationId thirdparty.applicationId
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lint {
        abortOnError false
    }
    namespace 'io.rapidpro.surveyor'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
    google()
}

dependencies {
    implementation project(':goflow')

    // google services
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.5'

    // rest api
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // for location services
    implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'

    // internationalization, language iso codes
    implementation 'com.neovisionaries:nv-i18n:1.11'

    // apache commons
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    //noinspection GradleDependency newer versions throw java.nio.channels.CancelledKeyException in tests for some reason
    implementation 'commons-io:commons-io:2.4'

    implementation 'com.vdurmont:semver4j:2.2.0'

    // Android M permissions
    implementation 'com.greysonparrelli.permiso:permiso:0.3.0'

    // unit tests
    testImplementation 'junit:junit:4.13.2'

    // instrumented tests
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.5.1'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
}

task jacocoMergedReport(type: JacocoReport, dependsOn: ['testRapidproDebugUnitTest', 'createRapidproDebugCoverageReport']) {

    reports {
        xml.required = true
        html.required = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    getSourceDirectories().setFrom(files([mainSrc]))
    getClassDirectories().setFrom(files([debugTree]))
    getExecutionData().setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testRapidproDebugUnitTest.exec",
            "outputs/code_coverage/rapidproDebugAndroidTest/connected/*coverage.ec"
    ]))
}
